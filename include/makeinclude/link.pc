OUTPUT_FORMAT("elf32-i386")
ENTRY(start)

physical_load_addr = (0x00100000);
virtual_load_addr  = (0xf8000000);
virtual_offset  	 = (virtual_load_addr - physical_load_addr);

SECTIONS
{
  . = physical_load_addr;
  . += virtual_offset;

  .text : AT(ADDR(.text) - virtual_offset)
  {
    start_text = .;
    *(.text*)
    end_text = .;
  }

  .rodata ALIGN (0x1000) : AT(ADDR(.rodata) - virtual_offset)
  {
    start_rodata = .;
    *(.rodata*)
    end_rodata = .;
  }

  .data ALIGN (0x1000) : AT(ADDR(.data) - virtual_offset)
  {
    start_data = .;
    start_ctors = .;
    *(.ctor*)
    end_ctors = .;
    start_dtors = .;
    *(.dtor*)
    end_dtors = .;
    *(.data*)
    *(.gnu*)
    *(.note*)
    *(.comment*)
    *(COMMON)
    end_data = .;
  }

  .bss : AT(ADDR(.bss) - virtual_offset)
  {
    start_bss = .;
    *(.bss*)
    end_bss = .;
  }
}
